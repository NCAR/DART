#!/bin/csh
#
# DART software - Copyright 2004 - 2013 UCAR. This open source software is
# provided by UCAR, "as is", without charge, subject to all terms of use at
# http://www.image.ucar.edu/DAReS/DART/DART_download
#
# DART $Id: mkmf_perfect_model_obs 9547 2016-01-20 16:53:46Z hkershaw $
#
# usage: mkmf_perfect_model_obs [ -mpi | -nompi ]
#
# without any args, builds perfect_model_obs without mpi libraries, and it will run
# as a normal executable. if -mpi is given, it will be compiled with the mpi
# libraries and can run with multiple cooperating processes.

if ( $#argv > 0 ) then
  if ("$argv[1]" == "-mpi") then
    setenv usingmpi 1
  else if ("$argv[1]" == "-nompi") then
    setenv usingmpi 0
  else
    echo "Unrecognized argument to mkmf_perfect_model_obs: $argv[1]"
    echo "Usage: mkmf_perfect_model_obs [ -mpi | -nompi ]"
    echo " default is to generate a Makefile without MPI support."
    exit -1
  endif
else
  setenv usingmpi 0
endif


# make a backup copy of the path_names file, and then use
# sed to make sure it includes either the non-mpi subroutines,
# or the subroutines which really call mpi.
cp -f path_names_perfect_model_obs path_names_perfect_model_obs.back

if ( $usingmpi ) then

  echo "Making Makefile with MPI"
  touch using_mpi_for_perfect_model_obs
  sed -e 's;/null_mpi_util;/mpi_util;' path_names_perfect_model_obs.back >! path_names_perfect_model_obs
  cp -f path_names_perfect_model_obs path_names_perfect_model_obs.temp
  sed -e 's;/null_win_mod;/no_cray_win_mod;' path_names_perfect_model_obs.temp >! path_names_perfect_model_obs

  setenv wrapper_arg -w

else

  echo "Making Makefile without MPI"
  rm -f using_mpi_for_perfect_model_obs
  sed -e 's;/mpi_util;/null_mpi_util;' path_names_perfect_model_obs.back >! path_names_perfect_model_obs
  setenv wrapper_arg ""
  cp -f path_names_perfect_model_obs path_names_perfect_model_obs.temp
  sed '\,distributed/no_cray_win_mod,d' path_names_perfect_model_obs.temp >! path_names_perfect_model_obs
  cp -f path_names_perfect_model_obs path_names_perfect_model_obs.temp
  sed '\,distributed/cray_win_mod,d' path_names_perfect_model_obs.temp >! path_names_perfect_model_obs
  set p=`grep distributed/null_win_mod.f90 path_names_perfect_model_obs | wc -w`
  if ( $p == 0) then
  #  echo distributed/null_win_mod.f90 >> path_names_perfect_model_obs
     echo 'distributed/null_win_mod.f90' | cat - path_names_perfect_model_obs > temp && mv temp path_names_perfect_model_obs
  endif

endif

# remove temp file and now really call mkmf to generate makefile
rm -f path_names_perfect_model_obs.back

../../../mkmf/mkmf -p perfect_model_obs -t ../../../mkmf/mkmf.template   \
        -a "../../.." ${wrapper_arg} path_names_perfect_model_obs

exit $status

# <next few lines under version control, do not edit>
# $URL: https://proxy.subversion.ucar.edu/DAReS/DART/branches/rma_cice/models/clm/work/mkmf_perfect_model_obs $
# $Revision: 9547 $
# $Date: 2016-01-20 08:53:46 -0800 (Wed, 20 Jan 2016) $

