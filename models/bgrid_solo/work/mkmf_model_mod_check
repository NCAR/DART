#!/bin/csh
#
# DART software - Copyright 2004 - 2013 UCAR. This open source software is
# provided by UCAR, "as is", without charge, subject to all terms of use at
# http://www.image.ucar.edu/DAReS/DART/DART_download
#
# DART $Id: mkmf_model_mod_check 9181 2015-12-03 20:56:54Z hendric $
#
# usage: mkmf_model_mod_check [ -mpi | -nompi ]
#
# without any args, builds model_mod_check without mpi libraries, and it will run
# as a normal executable. if -mpi is given, it will be compiled with the mpi
# libraries and can run with multiple cooperating processes.

if ( $#argv > 0 ) then
  if ("$argv[1]" == "-mpi") then
    setenv usingmpi 1
  else if ("$argv[1]" == "-nompi") then
    setenv usingmpi 0
  else
    echo "Unrecognized argument to mkmf_model_mod_check: $argv[1]"
    echo "Usage: mkmf_model_mod_check [ -mpi | -nompi ]"
    echo " default is to generate a Makefile without MPI support."
    exit -1
  endif
else
  setenv usingmpi 0
endif


# make a backup copy of the path_names file, and then use
# sed to make sure it includes either the non-mpi subroutines,
# or the subroutines which really call mpi.
cp -f path_names_model_mod_check path_names_model_mod_check.back

if ( $usingmpi ) then

  echo "Making Makefile with MPI"
  touch using_mpi_for_model_mod_check
  sed -e 's;/null_mpi_util;/mpi_util;' path_names_model_mod_check.back >! path_names_model_mod_check
  cp -f path_names_model_mod_check path_names_model_mod_check.temp
  sed -e 's;/null_win_mod;/no_cray_win_mod;' path_names_model_mod_check.temp >! path_names_model_mod_check

  setenv wrapper_arg -w

else

  echo "Making Makefile without MPI"
  rm -f using_mpi_for_model_mod_check
  sed -e 's;/mpi_util;/null_mpi_util;' path_names_model_mod_check.back >! path_names_model_mod_check
  setenv wrapper_arg ""
  cp -f path_names_model_mod_check path_names_model_mod_check.temp
  sed '\,distributed/no_cray_win_mod,d' path_names_model_mod_check.temp >! path_names_model_mod_check
  cp -f path_names_model_mod_check path_names_model_mod_check.temp
  sed '\,distributed/cray_win_mod,d' path_names_model_mod_check.temp >! path_names_model_mod_check
  set p=`grep distributed/null_win_mod.f90 path_names_model_mod_check | wc -w`
  if ( $p == 0) then
  #  echo distributed/null_win_mod.f90 >> path_names_model_mod_check
     echo 'distributed/null_win_mod.f90' | cat - path_names_model_mod_check > temp && mv temp path_names_model_mod_check
  endif

endif

# remove temp file and now really call mkmf to generate makefile
rm -f path_names_model_mod_check.back

../../../mkmf/mkmf -p model_mod_check -t ../../../mkmf/mkmf.template   \
        -a "../../.." ${wrapper_arg} path_names_model_mod_check

exit $status

# <next few lines under version control, do not edit>
# $URL: https://proxy.subversion.ucar.edu/DAReS/DART/branches/rma_trunk_mmc_fix/models/wrf/work/mkmf_model_mod_check $
# $Revision: 9181 $
# $Date: 2015-12-03 13:56:54 -0700 (Thu, 03 Dec 2015) $

