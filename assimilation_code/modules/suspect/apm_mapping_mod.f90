module apm_mapping_mod
   implicit none
   private

   public :: w3fb06, &
             w3fb07, &
             w3fb08, &
             w3fb09, &
             w3fb11, &
             w3fb12, &
             w3fb13, &
             w3fb14

   contains
             
      SUBROUTINE W3FB06(ALAT,ALON,ALAT1,ALON1,DX,ALONV,XI,XJ)
!$$$   SUBPROGRAM  DOCUMENTATION  BLOCK
!
! SUBPROGRAM:  W3FB06        LAT/LON TO POLA (I,J) FOR GRIB
!   PRGMMR: STACKPOLE        ORG: NMC42       DATE:88-04-05
!
! ABSTRACT: CONVERTS THE COORDINATES OF A LOCATION ON EARTH GIVEN IN
!   THE NATURAL COORDINATE SYSTEM OF LATITUDE/LONGITUDE TO A GRID
!   COORDINATE SYSTEM OVERLAID ON A POLAR STEREOGRAPHIC MAP PRO-
!   JECTION TRUE AT 60 DEGREES N OR S LATITUDE. W3FB06 IS THE REVERSE
!   OF W3FB07. USES GRIB SPECIFICATION OF THE LOCATION OF THE GRID
!
! PROGRAM HISTORY LOG:
!   88-01-01  ORIGINAL AUTHOR:  STACKPOLE, W/NMC42
!   90-04-12  R.E.JONES   CONVERT TO CRAY CFT77 FORTRAN
!
! USAGE:  CALL W3FB06 (ALAT,ALON,ALAT1,ALON1,DX,ALONV,XI,XJ)
!   INPUT ARGUMENT LIST:
!     ALAT     - LATITUDE IN DEGREES (NEGATIVE IN SOUTHERN HEMIS)
!     ALON     - EAST LONGITUDE IN DEGREES, REAL*4
!     ALAT1    - LATITUDE  OF LOWER LEFT POINT OF GRID (POINT (1,1))
!     ALON1    - LONGITUDE OF LOWER LEFT POINT OF GRID (POINT (1,1))
!                ALL REAL*4
!     DX       - MESH LENGTH OF GRID IN METERS AT 60 DEG LAT
!                 MUST BE SET NEGATIVE IF USING
!                 SOUTHERN HEMISPHERE PROJECTION.
!                   190500.0 LFM GRID,
!                   381000.0 NH PE GRID, -381000.0 SH PE GRID, ETC.
!     ALONV    - THE ORIENTATION OF THE GRID.  I.E.,
!                THE EAST LONGITUDE VALUE OF THE VERTICAL MERIDIAN
!                WHICH IS PARALLEL TO THE Y-AXIS (OR COLUMNS OF
!                OF THE GRID)ALONG WHICH LATITUDE INCREASES AS
!                THE Y-COORDINATE INCREASES.  REAL*4
!                   FOR EXAMPLE:
!                   255.0 FOR LFM GRID,
!                   280.0 NH PE GRID, 100.0 SH PE GRID, ETC.
!
!   OUTPUT ARGUMENT LIST:
!     XI       - I COORDINATE OF THE POINT SPECIFIED BY ALAT, ALON
!     XJ       - J COORDINATE OF THE POINT BOTH REAL*4
!
!   REMARKS: FORMULAE AND NOTATION LOOSELY BASED ON HOKE, HAYES,
!     AND RENNINGER'S "MAP PROJECTIONS AND GRID SYSTEMS...", MARCH 1981
!     AFGWC/TN-79/003
!
! ATTRIBUTES:
!   LANGUAGE: CRAY CFT77 FORTRAN
!   MACHINE:  CRAY Y-MP8/832
!$$$
         implicit none
         real RERTH,SS60,PI
         real ALAT,ALON,ALAT1,ALON1,DX,ALONV,XI,XJ
         real H,DXL,REFLON,RADPD,REBYDX
         real ALA,ALO,ALA1,ALO1,RM,RMLL,POLEI,POLEJ
!
         RERTH = 6.3712E+6
         PI    = 3.1416
         SS60  = 1.86603
!
!        PRELIMINARY VARIABLES AND REDIFINITIONS
!
!        H = 1 FOR NORTHERN HEMISPHERE; = -1 FOR SOUTHERN
!
!        REFLON IS LONGITUDE UPON WHICH THE POSITIVE X-COORDINATE
!        DRAWN THROUGH THE POLE AND TO THE RIGHT LIES
!        ROTATED AROUND FROM ORIENTATION (Y-COORDINATE) LONGITUDE
!        DIFFERENTLY IN EACH HEMISPHERE
!
         IF (DX.LT.0) THEN
           H      = -1.0
           DXL    = -DX
           REFLON = ALONV - 90.0
         ELSE
           H      = 1.0
           DXL    = DX
           REFLON = ALONV - 270.0
         ENDIF
!
         RADPD  = PI / 180.0
         REBYDX = RERTH/DXL
!
!        RADIUS TO LOWER LEFT HAND (LL) CORNER
!
         ALA1 =  ALAT1 * RADPD
         RMLL = REBYDX * COS(ALA1) * SS60/(1. + H * SIN(ALA1))
!
!        USE LL POINT INFO TO LOCATE POLE POINT
!
         ALO1  = (ALON1 - REFLON) * RADPD
         POLEI = 1. - RMLL * COS(ALO1)
         POLEJ = 1. - H * RMLL * SIN(ALO1)
!
!        RADIUS TO DESIRED POINT AND THE I J TOO
!
         ALA = ALAT   * RADPD
         RM  = REBYDX * COS(ALA) * SS60/(1. + H * SIN(ALA))
!
         ALO = (ALON - REFLON) * RADPD
         XI  = POLEI + RM * COS(ALO)
         XJ  = POLEJ + H * RM * SIN(ALO)
!
      RETURN
      END SUBROUTINE W3FB06

!-------------------------------------------------------------------------------

      SUBROUTINE W3FB07(XI,XJ,ALAT1,ALON1,DX,ALONV,ALAT,ALON)
!$$$   SUBPROGRAM  DOCUMENTATION  BLOCK
!
! SUBPROGRAM:  W3FB07        GRID COORDS TO LAT/LON FOR GRIB
!   PRGMMR: STACKPOLE        ORG: NMC42       DATE:88-04-05
!
! ABSTRACT: CONVERTS THE COORDINATES OF A LOCATION ON EARTH GIVEN IN A
!   GRID COORDINATE SYSTEM OVERLAID ON A POLAR STEREOGRAPHIC MAP PRO-
!   JECTION TRUE AT 60 DEGREES N OR S LATITUDE TO THE
!   NATURAL COORDINATE SYSTEM OF LATITUDE/LONGITUDE
!   W3FB07 IS THE REVERSE OF W3FB06.
!   USES GRIB SPECIFICATION OF THE LOCATION OF THE GRID
!
! PROGRAM HISTORY LOG:
!   88-01-01  ORIGINAL AUTHOR:  STACKPOLE, W/NMC42
!   90-04-12  R.E.JONES   CONVERT TO CRAY CFT77 FORTRAN
!
! USAGE:  CALL W3FB07(XI,XJ,ALAT1,ALON1,DX,ALONV,ALAT,ALON)
!   INPUT ARGUMENT LIST:
!     XI       - I COORDINATE OF THE POINT  REAL*4
!     XJ       - J COORDINATE OF THE POINT  REAL*4
!     ALAT1    - LATITUDE  OF LOWER LEFT POINT OF GRID (POINT 1,1)
!                LATITUDE &lt;0 FOR SOUTHERN HEMISPHERE; REAL*4
!     ALON1    - LONGITUDE OF LOWER LEFT POINT OF GRID (POINT 1,1)
!                  EAST LONGITUDE USED THROUGHOUT; REAL*4
!     DX       - MESH LENGTH OF GRID IN METERS AT 60 DEG LAT
!                 MUST BE SET NEGATIVE IF USING
!                 SOUTHERN HEMISPHERE PROJECTION; REAL*4
!                   190500.0 LFM GRID,
!                   381000.0 NH PE GRID, -381000.0 SH PE GRID, ETC.
!     ALONV    - THE ORIENTATION OF THE GRID.  I.E.,
!                THE EAST LONGITUDE VALUE OF THE VERTICAL MERIDIAN
!                WHICH IS PARALLEL TO THE Y-AXIS (OR COLUMNS OF
!                THE GRID) ALONG WHICH LATITUDE INCREASES AS
!                THE Y-COORDINATE INCREASES.  REAL*4
!                   FOR EXAMPLE:
!                   255.0 FOR LFM GRID,
!                   280.0 NH PE GRID, 100.0 SH PE GRID, ETC.
!
!   OUTPUT ARGUMENT LIST:
!     ALAT     - LATITUDE IN DEGREES (NEGATIVE IN SOUTHERN HEMI.)
!     ALON     - EAST LONGITUDE IN DEGREES, REAL*4
!
!   REMARKS: FORMULAE AND NOTATION LOOSELY BASED ON HOKE, HAYES,
!     AND RENNINGER'S "MAP PROJECTIONS AND GRID SYSTEMS...", MARCH 1981
!     AFGWC/TN-79/003
!
! ATTRIBUTES:
!   LANGUAGE: CRAY CFT77 FORTRAN
!   MACHINE:  CRAY Y-MP8/832
!$$$
         implicit none
         real RERTH,SS60,PI
         real ALAT,ALON,ALAT1,ALON1,DX,ALONV,XI,XJ
         real H,DXL,REFLON,RADPD,DEGPRD,REBYDX
         real ALA,ALO,ALA1,ALO1,RM,RMLL,POLEI,POLEJ
         real XX,YY,R2,GI2,ARCCOS
!
         RERTH = 6.3712E+6
         PI    = 3.1416
         SS60  = 1.86603
!
!        PRELIMINARY VARIABLES AND REDIFINITIONS
!
!        H = 1 FOR NORTHERN HEMISPHERE; = -1 FOR SOUTHERN
!
!        REFLON IS LONGITUDE UPON WHICH THE POSITIVE X-COORDINATE
!        DRAWN THROUGH THE POLE AND TO THE RIGHT LIES
!        ROTATED AROUND FROM ORIENTATION (Y-COORDINATE) LONGITUDE
!        DIFFERENTLY IN EACH HEMISPHERE
!
         IF (DX.LT.0) THEN
           H      = -1.0
           DXL    = -DX
           REFLON = ALONV - 90.0
         ELSE
           H      = 1.0
           DXL    = DX
           REFLON = ALONV - 270.0
         ENDIF
!
         RADPD  = PI    / 180.0
         DEGPRD = 180.0 / PI
         REBYDX = RERTH / DXL
!
!        RADIUS TO LOWER LEFT HAND (LL) CORNER
!
         ALA1 =  ALAT1 * RADPD
         RMLL = REBYDX * COS(ALA1) * SS60/(1. + H * SIN(ALA1))
!
!        USE LL POINT INFO TO LOCATE POLE POINT
!
         ALO1 = (ALON1 - REFLON) * RADPD
         POLEI = 1. - RMLL * COS(ALO1)
         POLEJ = 1. - H * RMLL * SIN(ALO1)
!
!        RADIUS TO THE I,J POINT (IN GRID UNITS)
!
         XX =  XI - POLEI
         YY = (XJ - POLEJ) * H
         R2 =  XX**2 + YY**2
!
!        NOW THE MAGIC FORMULAE
!
         IF (R2.EQ.0) THEN
           ALAT = H * 90.
           ALON = REFLON
         ELSE
           GI2    = (REBYDX * SS60)**2
           ALAT   = DEGPRD * H * ASIN((GI2 - R2)/(GI2 + R2))
           ARCCOS = ACOS(XX/SQRT(R2))
           IF (YY.GT.0) THEN
             ALON = REFLON + DEGPRD * ARCCOS
           ELSE
             ALON = REFLON - DEGPRD * ARCCOS
           ENDIF
         ENDIF
         IF (ALON.LT.0) ALON = ALON + 360.
!
      RETURN
      END SUBROUTINE W3FB07

!-------------------------------------------------------------------------------

      SUBROUTINE W3FB08(ALAT,ALON,ALAT1,ALON1,ALATIN,DX,XI,XJ)
!$$$   SUBPROGRAM  DOCUMENTATION  BLOCK
!
! SUBPROGRAM:  W3FB08        LAT/LON TO MERC (I,J) FOR GRIB
!   PRGMMR: STACKPOLE        ORG: NMC42       DATE:88-04-05
!
! ABSTRACT: CONVERTS A LOCATION ON EARTH GIVEN IN
!   THE COORDINATE SYSTEM OF LATITUDE/LONGITUDE TO AN (I,J)
!   COORDINATE SYSTEM OVERLAID ON A MERCATOR MAP PROJECTION
!   W3FB08 IS THE REVERSE OF W3FB09
!   USES GRIB SPECIFICATION OF THE LOCATION OF THE GRID
!
! PROGRAM HISTORY LOG:
!   88-03-01  ORIGINAL AUTHOR:  STACKPOLE, W/NMC42
!   90-04-12  R.E.JONES   CONVERT TO CRAY CFT77 FORTRAN
!
! USAGE:  CALL W3FB08 (ALAT,ALON,ALAT1,ALON1,ALATIN,DX,XI,XJ)
!   INPUT ARGUMENT LIST:
!     ALAT     - LATITUDE IN DEGREES (NEGATIVE IN SOUTHERN HEMIS)
!     ALON     - EAST LONGITUDE IN DEGREES, REAL*4
!     ALAT1    - LATITUDE  OF LOWER LEFT CORNER OF GRID (POINT (1,1))
!     ALON1    - LONGITUDE OF LOWER LEFT CORNER OF GRID (POINT (1,1))
!                ALL REAL*4
!     ALATIN   - THE LATITUDE AT WHICH THE MERCATOR CYLINDER
!                INTERSECTS THE EARTH
!     DX       - MESH LENGTH OF GRID IN METERS AT ALATIN
!
!   OUTPUT ARGUMENT LIST:
!     XI       - I COORDINATE OF THE POINT SPECIFIED BY ALAT, ALON
!     XJ       - J COORDINATE OF THE POINT; BOTH REAL*4
!
!   REMARKS: FORMULAE AND NOTATION LOOSELY BASED ON HOKE, HAYES,
!     AND RENNINGER'S "MAP PROJECTIONS AND GRID SYSTEMS...", MARCH 1981
!     AFGWC/TN-79/003
!
! ATTRIBUTES:
!   LANGUAGE: CRAY CFT77 FORTRAN
!   MACHINE:  CRAY Y-MP8/832
!$$$
         implicit none
         real ALAT,ALON,ALAT1,ALON1,ALATIN,DX,XI,XJ
         real RERTH,PI,RADPD,DEGPR,CLAIN,DELLON,DJEO
!
         RERTH  = 6.3712E+6
         PI     = 3.1416
!
!        PRELIMINARY VARIABLES AND REDIFINITIONS
!
         RADPD  = PI    / 180.0
         DEGPR  = 180.0 / PI
         CLAIN  = COS(RADPD*ALATIN)
         DELLON = DX   / (RERTH*CLAIN)
!
!        GET DISTANCE FROM EQUATOR TO ORIGIN ALAT1
!
         DJEO = 0.
         IF (ALAT1.NE.0.) &
          DJEO = (ALOG(TAN(0.5*((ALAT1+90.0)*RADPD))))/DELLON
!
!        NOW THE I AND J COORDINATES
!
         XI = 1. + ((ALON - ALON1)/(DELLON*DEGPR))
         XJ = 1. + (ALOG(TAN(0.5*((ALAT + 90.) * RADPD))))/ &
               DELLON - DJEO
!
      RETURN
      END SUBROUTINE W3FB08

!-------------------------------------------------------------------------------

      SUBROUTINE W3FB09(XI,XJ,ALAT1,ALON1,ALATIN,DX,ALAT,ALON)
!$$$   SUBPROGRAM  DOCUMENTATION  BLOCK
!
! SUBPROGRAM:  W3FB09        MERC (I,J) TO LAT/LON FOR GRIB
!   PRGMMR: STACKPOLE        ORG: NMC42       DATE:88-04-05
!
! ABSTRACT: CONVERTS A LOCATION ON EARTH GIVEN IN
!   AN I,J COORDINATE SYSTEM OVERLAID ON A MERCATOR MAP PROJECTION
!   TO THE COORDINATE SYSTEM OF LATITUDE/LONGITUDE
!   W3FB09 IS THE REVERSE OF W3FB08
!   USES GRIB SPECIFICATION OF THE LOCATION OF THE GRID
!
! PROGRAM HISTORY LOG:
!   88-03-01  ORIGINAL AUTHOR:  STACKPOLE, W/NMC42
!   90-04-12  R.E.JONES   CONVERT TO CRAY CFT77 FORTRAN
!
! USAGE:  CALL W3FB09 (XI,XJ,ALAT1,ALON1,ALATIN,DX,ALAT,ALON)
!   INPUT ARGUMENT LIST:
!     XI       - I COORDINATE OF THE POINT
!     XJ       - J COORDINATE OF THE POINT; BOTH REAL*4
!     ALAT1    - LATITUDE  OF LOWER LEFT CORNER OF GRID (POINT (1,1))
!     ALON1    - LONGITUDE OF LOWER LEFT CORNER OF GRID (POINT (1,1))
!                ALL REAL*4
!     ALATIN   - THE LATITUDE AT WHICH THE MERCATOR CYLINDER
!                INTERSECTS THE EARTH
!     DX       - MESH LENGTH OF GRID IN METERS AT ALATIN
!
!   OUTPUT ARGUMENT LIST:
!     ALAT     - LATITUDE IN DEGREES (NEGATIVE IN SOUTHERN HEMIS)
!     ALON     - EAST LONGITUDE IN DEGREES, REAL*4
!              - OF THE POINT SPECIFIED BY (I,J)
!
!   REMARKS: FORMULAE AND NOTATION LOOSELY BASED ON HOKE, HAYES,
!     AND RENNINGER'S "MAP PROJECTIONS AND GRID SYSTEMS...", MARCH 1981
!     AFGWC/TN-79/003
!
! ATTRIBUTES:
!   LANGUAGE: CRAY CFT77 FORTRAN
!   MACHINE:  CRAY Y-MP8/832
!$$$
         implicit none
         real ALAT,ALON,ALAT1,ALON1,ALATIN,DX,XI,XJ
         real RERTH,PI,RADPD,DEGPR,CLAIN,DELLON,DJEO
!
         RERTH  = 6.3712E+6
         PI     = 3.1416
!
!        PRELIMINARY VARIABLES AND REDIFINITIONS
!
         RADPD  = PI    / 180.0
         DEGPR  = 180.0 / PI
         CLAIN  = COS(RADPD*ALATIN)
         DELLON = DX   / (RERTH*CLAIN)
!
!        GET DISTANCE FROM EQUATOR TO ORIGIN ALAT1
!
         DJEO = 0.
         IF (ALAT1.NE.0.) &
          DJEO = (ALOG(TAN(0.5*((ALAT1+90.0)*RADPD))))/DELLON
!
!        NOW THE LAT AND LON
!
         ALAT = 2.0*ATAN(EXP(DELLON*(DJEO + XJ-1.)))*DEGPR - 90.0
         ALON = (XI-1.) * DELLON * DEGPR + ALON1
!
      RETURN
      END SUBROUTINE W3FB09

!-------------------------------------------------------------------------------

      SUBROUTINE W3FB11(ALAT,ELON,ALAT1,ELON1,DX,ELONV,ALATAN,XI,XJ)
!$$$   SUBPROGRAM  DOCUMENTATION  BLOCK
!
! SUBPROGRAM:  W3FB11        LAT/LON TO LAMBERT(I,J) FOR GRIB
!   PRGMMR: STACKPOLE        ORG: NMC42       DATE:88-11-28
!
! ABSTRACT: CONVERTS THE COORDINATES OF A LOCATION ON EARTH GIVEN IN
!   THE NATURAL COORDINATE SYSTEM OF LATITUDE/LONGITUDE TO A GRID
!   COORDINATE SYSTEM OVERLAID ON A LAMBERT CONFORMAL TANGENT CONE
!   PROJECTION TRUE AT A GIVEN N OR S LATITUDE. W3FB11 IS THE REVERSE
!   OF W3FB12. USES GRIB SPECIFICATION OF THE LOCATION OF THE GRID
!
! PROGRAM HISTORY LOG:
!   88-11-25  ORIGINAL AUTHOR:  STACKPOLE, W/NMC42
!   90-04-12  R.E.JONES   CONVERT TO CFT77 FORTRAN
!   94-04-28  R.E.JONES   ADD SAVE STATEMENT
!
! USAGE:  CALL W3FB11 (ALAT,ELON,ALAT1,ELON1,DX,ELONV,ALATAN,XI,XJ)
!   INPUT ARGUMENT LIST:
!     ALAT     - LATITUDE IN DEGREES (NEGATIVE IN SOUTHERN HEMIS)
!     ELON     - EAST LONGITUDE IN DEGREES, REAL*4
!     ALAT1    - LATITUDE  OF LOWER LEFT POINT OF GRID (POINT (1,1))
!     ELON1    - LONGITUDE OF LOWER LEFT POINT OF GRID (POINT (1,1))
!                ALL REAL*4
!     DX       - MESH LENGTH OF GRID IN METERS AT TANGENT LATITUDE
!     ELONV    - THE ORIENTATION OF THE GRID.  I.E.,
!                THE EAST LONGITUDE VALUE OF THE VERTICAL MERIDIAN
!                WHICH IS PARALLEL TO THE Y-AXIS (OR COLUMNS OF
!                OF THE GRID) ALONG WHICH LATITUDE INCREASES AS
!                THE Y-COORDINATE INCREASES.  REAL*4
!                THIS IS ALSO THE MERIDIAN (ON THE BACK SIDE OF THE
!                TANGENT CONE) ALONG WHICH THE CUT IS MADE TO LAY
!                THE CONE FLAT.
!     ALATAN   - THE LATITUDE AT WHICH THE LAMBERT CONE IS TANGENT TO
!                (TOUCHING) THE SPHERICAL EARTH.
!                 SET NEGATIVE TO INDICATE A
!                 SOUTHERN HEMISPHERE PROJECTION.
!
!   OUTPUT ARGUMENT LIST:
!     XI       - I COORDINATE OF THE POINT SPECIFIED BY ALAT, ELON
!     XJ       - J COORDINATE OF THE POINT; BOTH REAL*4
!
!   REMARKS: FORMULAE AND NOTATION LOOSELY BASED ON HOKE, HAYES,
!     AND RENNINGER'S "MAP PROJECTIONS AND GRID SYSTEMS...", MARCH 1981
!     AFGWC/TN-79/003
!
! ATTRIBUTES:
!   LANGUAGE: CRAY CFT77 FORTRAN
!   MACHINE:  CRAY C916-128, CRAY Y-MP8/864, CRAY Y-MP EL2/256
!$$$
         implicit none
         real ALAT,ELON,ALAT1,ELON1,DX,ELONV,ALATAN,XI,XJ
         real RERTH,PI,H,RADPD,REBYDX,ALATN1,AN,COSLTN,ELON1L
         real ELONL,ELONVR,ALA1,RMLL,ELO1,ARG,POLEI,POLEJ,ALA,RM,ELO
!
         RERTH  = 6.3712E+6
         PI     = 3.1416

!
!        PRELIMINARY VARIABLES AND REDIFINITIONS
!
!        H = 1 FOR NORTHERN HEMISPHERE; = -1 FOR SOUTHERN
!
         IF (ALATAN.GT.0) THEN
           H = 1.
         ELSE
           H = -1.
         ENDIF
!
         RADPD  = PI    / 180.0
         REBYDX = RERTH / DX
         ALATN1 = ALATAN * RADPD
         AN     = H * SIN(ALATN1)
         COSLTN = COS(ALATN1)

!        MAKE SURE THAT INPUT LONGITUDES DO NOT PASS THROUGH
!        THE CUT ZONE (FORBIDDEN TERRITORY) OF THE FLAT MAP
!        AS MEASURED FROM THE VERTICAL (REFERENCE) LONGITUDE.
!
         ELON1L = ELON1
         IF ((ELON1 - ELONV).GT.180.) &
          ELON1L = ELON1 - 360.
         IF ((ELON1 - ELONV).LT.(-180.)) &
          ELON1L = ELON1 + 360.
!
         ELONL = ELON
         IF ((ELON  - ELONV).GT.180.) &
          ELONL  = ELON  - 360.
         IF ((ELON - ELONV).LT.(-180.)) &
          ELONL = ELON + 360.
!
         ELONVR = ELONV * RADPD
!
!        RADIUS TO LOWER LEFT HAND (LL) CORNER
!
         ALA1 =  ALAT1 * RADPD
         RMLL = REBYDX * (((COSLTN)**(1.-AN))*(1.+AN)**AN) * &
                (((COS(ALA1))/(1.+H*SIN(ALA1)))**AN)/AN
!
!        USE LL POINT INFO TO LOCATE POLE POINT
!
         ELO1 = ELON1L * RADPD
         ARG = AN * (ELO1-ELONVR)
         POLEI = 1. - H * RMLL * SIN(ARG)
         POLEJ = 1. + RMLL * COS(ARG)
!
!        RADIUS TO DESIRED POINT AND THE I J TOO
!
         ALA =  ALAT * RADPD
         RM = REBYDX * ((COSLTN**(1.-AN))*(1.+AN)**AN) * &
              (((COS(ALA))/(1.+H*SIN(ALA)))**AN)/AN
!
         ELO = ELONL * RADPD
         ARG = AN*(ELO-ELONVR)
         XI = POLEI + H * RM * SIN(ARG)
         XJ = POLEJ - RM * COS(ARG)
!
!        IF COORDINATE LESS THAN 1
!        COMPENSATE FOR ORIGIN AT (1,1)
!
         IF (XI.LT.1.)  XI = XI - 1.
         IF (XJ.LT.1.)  XJ = XJ - 1.
!
      RETURN
      END SUBROUTINE W3FB11

!-------------------------------------------------------------------------------

      SUBROUTINE W3FB12(XI,XJ,ALAT1,ELON1,DX,ELONV,ALATAN,ALAT,ELON, &
                 IERR)
!$$$   SUBPROGRAM  DOCUMENTATION  BLOCK
!
! SUBPROGRAM:  W3FB12        LAMBERT(I,J) TO LAT/LON FOR GRIB
!   PRGMMR: STACKPOLE        ORG: NMC42       DATE:88-11-28
!
! ABSTRACT: CONVERTS THE COORDINATES OF A LOCATION ON EARTH GIVEN IN A
!   GRID COORDINATE SYSTEM OVERLAID ON A LAMBERT CONFORMAL TANGENT
!   CONE PROJECTION TRUE AT A GIVEN N OR S LATITUDE TO THE
!   NATURAL COORDINATE SYSTEM OF LATITUDE/LONGITUDE
!   W3FB12 IS THE REVERSE OF W3FB11.
!   USES GRIB SPECIFICATION OF THE LOCATION OF THE GRID
!
! PROGRAM HISTORY LOG:
!   88-11-25  ORIGINAL AUTHOR:  STACKPOLE, W/NMC42
!   90-04-12  R.E.JONES   CONVERT TO CFT77 FORTRAN
!   94-04-28  R.E.JONES   ADD SAVE STATEMENT
!
! USAGE:  CALL W3FB12(XI,XJ,ALAT1,ELON1,DX,ELONV,ALATAN,ALAT,ELON,IERR,
!                                   IERR)
!   INPUT ARGUMENT LIST:
!     XI       - I COORDINATE OF THE POINT  REAL*4
!     XJ       - J COORDINATE OF THE POINT  REAL*4
!     ALAT1    - LATITUDE  OF LOWER LEFT POINT OF GRID (POINT 1,1)
!                LATITUDE &lt;0 FOR SOUTHERN HEMISPHERE; REAL*4
!     ELON1    - LONGITUDE OF LOWER LEFT POINT OF GRID (POINT 1,1)
!                  EAST LONGITUDE USED THROUGHOUT; REAL*4
!     DX       - MESH LENGTH OF GRID IN METERS AT TANGENT LATITUDE
!     ELONV    - THE ORIENTATION OF THE GRID.  I.E.,
!                THE EAST LONGITUDE VALUE OF THE VERTICAL MERIDIAN
!                WHICH IS PARALLEL TO THE Y-AXIS (OR COLUMNS OF
!                THE GRID) ALONG WHICH LATITUDE INCREASES AS
!                THE Y-COORDINATE INCREASES.  REAL*4
!                THIS IS ALSO THE MERIDIAN (ON THE OTHER SIDE OF THE
!                TANGENT CONE) ALONG WHICH THE CUT IS MADE TO LAY
!                THE CONE FLAT.
!     ALATAN   - THE LATITUDE AT WHICH THE LAMBERT CONE IS TANGENT TO
!                (TOUCHES OR OSCULATES) THE SPHERICAL EARTH.
!                 SET NEGATIVE TO INDICATE A
!                 SOUTHERN HEMISPHERE PROJECTION; REAL*4
!
!   OUTPUT ARGUMENT LIST:
!     ALAT     - LATITUDE IN DEGREES (NEGATIVE IN SOUTHERN HEMI.)
!     ELON     - EAST LONGITUDE IN DEGREES, REAL*4
!     IERR     - .EQ. 0   IF NO PROBLEM
!                .GE. 1   IF THE REQUESTED XI,XJ POINT IS IN THE
!                         FORBIDDEN ZONE, I.E. OFF THE LAMBERT MAP
!                         IN THE OPEN SPACE WHERE THE CONE IS CUT.
!                  IF IERR.GE.1 THEN ALAT=999. AND ELON=999.
!
!   REMARKS: FORMULAE AND NOTATION LOOSELY BASED ON HOKE, HAYES,
!     AND RENNINGER'S "MAP PROJECTIONS AND GRID SYSTEMS...", MARCH 1981
!     AFGWC/TN-79/003
!
! ATTRIBUTES:
!   LANGUAGE: CRAY CFT77 FORTRAN
!   MACHINE:  CRAY C916-128, CRAY Y-MP8/864, CRAY Y-MP EL2/256
!$$$
         implicit none
         real ALAT,ELON,ALAT1,ELON1,DX,ELONV,ALATAN,XI,XJ
         real RERTH,PI,OLDRML,H,PIBY2,RADPD,DEGPRD,REBYDX,ALATN1,AN
         real COSLTN,ELON1L,ELONVR,ALA1,RMLL,ELO1,ARG,POLEI,POLEJ
         real XX,YY,R2,THETA,BETA,ANINV,ANINV2,THING
         integer IERR
         logical NEWMAP
!
         RERTH  = 6.3712E+6
         PI     = 3.1416
         OLDRML = 99999.
!
!        PRELIMINARY VARIABLES AND REDIFINITIONS
!
!        H = 1 FOR NORTHERN HEMISPHERE; = -1 FOR SOUTHERN
!
         IF (ALATAN.GT.0) THEN
           H = 1.
         ELSE
           H = -1.
         ENDIF
!
         PIBY2  = PI     / 2.0
         RADPD  = PI     / 180.0
         DEGPRD = 1.0    / RADPD
         REBYDX = RERTH  / DX
         ALATN1 = ALATAN * RADPD
         AN     = H * SIN(ALATN1)
         COSLTN = COS(ALATN1)
!
!        MAKE SURE THAT INPUT LONGITUDE DOES NOT PASS THROUGH
!        THE CUT ZONE (FORBIDDEN TERRITORY) OF THE FLAT MAP
!        AS MEASURED FROM THE VERTICAL (REFERENCE) LONGITUDE
!
         ELON1L = ELON1
         IF ((ELON1-ELONV).GT.180.) &
          ELON1L = ELON1 - 360.
         IF ((ELON1-ELONV).LT.(-180.)) &
          ELON1L = ELON1 + 360.
!
         ELONVR = ELONV * RADPD
!
!        RADIUS TO LOWER LEFT HAND (LL) CORNER
!
         ALA1 =  ALAT1 * RADPD
         RMLL = REBYDX * ((COSLTN**(1.-AN))*(1.+AN)**AN) * &
                (((COS(ALA1))/(1.+H*SIN(ALA1)))**AN)/AN
!
!        USE RMLL TO TEST IF MAP AND GRID UNCHANGED FROM PREVIOUS
!        CALL TO THIS CODE.  THUS AVOID UNNEEDED RECOMPUTATIONS.
!
         IF (RMLL.EQ.OLDRML) THEN
           NEWMAP = .FALSE.
         ELSE
           NEWMAP = .TRUE.
           OLDRML = RMLL
!
!          USE LL POINT INFO TO LOCATE POLE POINT
!
           ELO1 = ELON1L * RADPD
           ARG = AN * (ELO1-ELONVR)
           POLEI = 1. - H * RMLL * SIN(ARG)
           POLEJ = 1. + RMLL * COS(ARG)
         ENDIF
!
!        RADIUS TO THE I,J POINT (IN GRID UNITS)
!              YY REVERSED SO POSITIVE IS DOWN
!
         XX = XI - POLEI
         YY = POLEJ - XJ
         R2 = XX**2 + YY**2
!
!        CHECK THAT THE REQUESTED I,J IS NOT IN THE FORBIDDEN ZONE
!           YY MUST BE POSITIVE UP FOR THIS TEST
!
         THETA = PI*(1.-AN)
         BETA = ABS(ATAN2(XX,-YY))
         IERR = 0
         IF (BETA.LE.THETA) THEN
           IERR = 1
           ALAT = 999.
           ELON = 999.
           IF (.NOT.NEWMAP)  RETURN
         ENDIF
!
!        NOW THE MAGIC FORMULAE
!
         IF (R2.EQ.0) THEN
           ALAT = H * 90.0
           ELON = ELONV
         ELSE
!
!          FIRST THE LONGITUDE
!
           ELON = ELONV + DEGPRD * ATAN2(H*XX,YY)/AN
           ELON = AMOD(ELON+360., 360.)
!
!          NOW THE LATITUDE
!          RECALCULATE THE THING ONLY IF MAP IS NEW SINCE LAST TIME
!
           IF (NEWMAP) THEN
             ANINV = 1./AN
             ANINV2 = ANINV/2.
             THING = ((AN/REBYDX) ** ANINV)/ &
              ((COSLTN**((1.-AN)*ANINV))*(1.+ AN))
           ENDIF
           ALAT = H*(PIBY2 - 2.*ATAN(THING*(R2**ANINV2)))*DEGPRD
         ENDIF
!
!        FOLLOWING TO ASSURE ERROR VALUES IF FIRST TIME THRU
!         IS OFF THE MAP
!
         IF (IERR.NE.0) THEN
           ALAT = 999.
           ELON = 999.
           IERR = 2
         ENDIF
      RETURN
      END SUBROUTINE W3FB12

!-------------------------------------------------------------------------------

      SUBROUTINE W3FB13(ALAT,ELON,ALAT1,ELON1,DX,ELONV,ALATAN1,ALATAN2,XI,XJ)
!$$$   SUBPROGRAM  DOCUMENTATION  BLOCK
!
! SUBPROGRAM:  W3FB13        LAT/LON TO LAMBERT(I,J) FOR GRIB
!   PRGMMR: STACKPOLE        ORG: NMC42       DATE:88-11-28
!
! ABSTRACT: CONVERTS THE COORDINATES OF A LOCATION ON EARTH GIVEN IN
!   THE NATURAL COORDINATE SYSTEM OF LATITUDE/LONGITUDE TO A GRID
!   COORDINATE SYSTEM OVERLAID ON A LAMBERT CONFORMAL CONE
!   PROJECTION TRUE AT A GIVEN N OR S LATITUDE. W3FB13 IS THE REVERSE
!   OF W3FB14. USES GRIB SPECIFICATION OF THE LOCATION OF THE GRID
!
! PROGRAM HISTORY LOG:
!   88-11-25  ORIGINAL AUTHOR:  STACKPOLE, W/NMC42
!   90-04-12  R.E.JONES   CONVERT TO CFT77 FORTRAN
!   94-04-28  R.E.JONES   ADD SAVE STATEMENT
! 2003-06-21  GILBERT     MODIFIED FROM W3FB11 AND ADDED SUPPORT FOR
!                         SECANT CONE AS WELL AS TANGENTIAL.
!
! USAGE:  CALL W3FB13 (ALAT,ELON,ALAT1,ELON1,DX,ELONV,ALATAN1,ALATAN2,XI,XJ)
!   INPUT ARGUMENT LIST:
!     ALAT     - LATITUDE IN DEGREES (NEGATIVE IN SOUTHERN HEMIS)
!     ELON     - EAST LONGITUDE IN DEGREES, REAL*4
!     ALAT1    - LATITUDE  OF LOWER LEFT POINT OF GRID (POINT (1,1))
!     ELON1    - LONGITUDE OF LOWER LEFT POINT OF GRID (POINT (1,1))
!                ALL REAL*4
!     DX       - MESH LENGTH OF GRID IN METERS AT TANGENT LATITUDE
!     ELONV    - THE ORIENTATION OF THE GRID.  I.E.,
!                THE EAST LONGITUDE VALUE OF THE VERTICAL MERIDIAN
!                WHICH IS PARALLEL TO THE Y-AXIS (OR COLUMNS OF
!                OF THE GRID) ALONG WHICH LATITUDE INCREASES AS
!                THE Y-COORDINATE INCREASES.  REAL*4
!                THIS IS ALSO THE MERIDIAN (ON THE BACK SIDE OF THE
!                TANGENT CONE) ALONG WHICH THE CUT IS MADE TO LAY
!                THE CONE FLAT.
!     ALATAN1  - THE 1ST LATITUDE FROM THE POLE AT WHICH THE LAMBERT CONE 
!                INTERSECTS THE SPHERICAL EARTH.
!                SET NEGATIVE TO INDICATE A SOUTHERN HEMISPHERE PROJECTION.
!                (IF ALATAN1.EQ.ALATAN2 PROJECTION IS ON TANGENT CONE)
!     ALATAN2  - THE 2ND LATITUDE FROM THE POLE AT WHICH THE LAMBERT CONE 
!                INTERSECTS THE SPHERICAL EARTH.
!                SET NEGATIVE TO INDICATE A SOUTHERN HEMISPHERE PROJECTION.
!
!   OUTPUT ARGUMENT LIST:
!     XI       - I COORDINATE OF THE POINT SPECIFIED BY ALAT, ELON
!     XJ       - J COORDINATE OF THE POINT; BOTH REAL*4
!
!   REMARKS: FORMULAE AND NOTATION LOOSELY BASED ON HOKE, HAYES,
!     AND RENNINGER'S "MAP PROJECTIONS AND GRID SYSTEMS...", MARCH 1981
!     AFGWC/TN-79/003
!
! ATTRIBUTES:
!   LANGUAGE: CRAY CFT77 FORTRAN
!   MACHINE:  CRAY C916-128, CRAY Y-MP8/864, CRAY Y-MP EL2/256
!$$$
         implicit none
         real ALAT,ELON,ALAT1,ELON1,DX,ELONV,ALATAN1,ALATAN2
         real RERTH,PI,H,RADPD,REBYDX,ALATN1,ALATN2,AN,COSLTN
         real ELONL,ELON1L,ELONVR,ALA,ALA1,PSI,RMLL,ELO1,ARG,POLEI,POLEJ
         real RM,ELO,XI,XJ
!
         RERTH  = 6.3712E+6
         PI     = 3.14159
!
!        PRELIMINARY VARIABLES AND REDIFINITIONS
!
!        H = 1 FOR NORTHERN HEMISPHERE; = -1 FOR SOUTHERN
!
         IF (ALATAN1.GT.0) THEN
           H = 1.
         ELSE
           H = -1.
         ENDIF
!
         RADPD  = PI    / 180.0
         REBYDX = RERTH / DX
         ALATN1 = ALATAN1 * RADPD
         ALATN2 = ALATAN2 * RADPD
         IF (ALATAN1.EQ.ALATAN2) THEN
            AN     = H * SIN(ALATN1)
         ELSE
           AN=LOG(COS(ALATN1)/COS(ALATN2))/ &
              LOG(TAN(((H*PI/2.)-ALATN1)/2.)/TAN(((H*PI/2.)-ALATN2)/2.))
         ENDIF
         COSLTN = COS(ALATN2)
!
!        MAKE SURE THAT INPUT LONGITUDES DO NOT PASS THROUGH
!        THE CUT ZONE (FORBIDDEN TERRITORY) OF THE FLAT MAP
!        AS MEASURED FROM THE VERTICAL (REFERENCE) LONGITUDE.
!
         ELON1L = ELON1
         IF ((ELON1 - ELONV).GT.180.) ELON1L = ELON1 - 360.
         IF ((ELON1 - ELONV).LT.(-180.)) ELON1L = ELON1 + 360.
!
         ELONL = ELON
         IF ((ELON  - ELONV).GT.180.) ELONL  = ELON  - 360.
         IF ((ELON - ELONV).LT.(-180.)) ELONL = ELON + 360.
!
         ELONVR = ELONV * RADPD
!
!        RADIUS TO LOWER LEFT HAND (LL) CORNER
!
         ALA1 =  ALAT1 * RADPD
!         RMLL = REBYDX * (((COSLTN)**(1.-AN))*(1.+AN)**AN) * &
!                (((COS(ALA1))/(1.+H*SIN(ALA1)))**AN)/AN
         PSI=(REBYDX*COSLTN)/(AN*(TAN((PI/4.)-(H*ALATN2/2.))**AN))
         RMLL=PSI*(TAN((PI/4.)-(H*ALA1/2.))**AN)
!
!        USE LL POINT INFO TO LOCATE POLE POINT
!
         ELO1 = ELON1L * RADPD
         ARG = AN * (ELO1-ELONVR)
         POLEI = 1. - H * RMLL * SIN(ARG)
         POLEJ = 1. + RMLL * COS(ARG)

!
!        RADIUS TO DESIRED POINT AND THE I J TOO
!
         ALA =  ALAT * RADPD
!         RM = REBYDX * ((COSLTN**(1.-AN))*(1.+AN)**AN) * &
!              (((COS(ALA))/(1.+H*SIN(ALA)))**AN)/AN
!
         RM=PSI*(TAN((PI/4.)-(H*ALA/2.))**AN)
         ELO = ELONL * RADPD
         ARG = AN*(ELO-ELONVR)
         XI = POLEI + H * RM * SIN(ARG)
         XJ = POLEJ - RM * COS(ARG)
!
!        IF COORDINATE LESS THAN 1
!        COMPENSATE FOR ORIGIN AT (1,1)
!
         IF (NINT(XI).LT.1)  XI = XI - 1.
         IF (NINT(XJ).LT.1)  XJ = XJ - 1.
!
         RETURN         
      END SUBROUTINE W3FB13

!-------------------------------------------------------------------------------

      SUBROUTINE W3FB14(XI,XJ,ALAT1,ELON1,DX,ELONV,ALATAN1, &
                        ALATAN2,ALAT,ELON,IERR)
!$$$   SUBPROGRAM  DOCUMENTATION  BLOCK
!
! SUBPROGRAM:  W3FB14        LAMBERT(I,J) TO LAT/LON FOR GRIB
!   PRGMMR: STACKPOLE        ORG: NMC42       DATE:88-11-28
!
! ABSTRACT: CONVERTS THE COORDINATES OF A LOCATION ON EARTH GIVEN IN A
!   GRID COORDINATE SYSTEM OVERLAID ON A LAMBERT CONFORMAL
!   CONE PROJECTION TRUE AT A GIVEN N OR S LATITUDE TO THE
!   NATURAL COORDINATE SYSTEM OF LATITUDE/LONGITUDE
!   W3FB14 IS THE REVERSE OF W3FB13.
!   USES GRIB SPECIFICATION OF THE LOCATION OF THE GRID
!
! PROGRAM HISTORY LOG:
!   88-11-25  ORIGINAL AUTHOR:  STACKPOLE, W/NMC42
!   90-04-12  R.E.JONES   CONVERT TO CFT77 FORTRAN
!   94-04-28  R.E.JONES   ADD SAVE STATEMENT
! 2003-06-21  GILBERT     MODIFIED FROM W3FB12 AND ADDED SUPPORT FOR
!                         SECANT CONE AS WELL AS TANGENTIAL.
!
! USAGE:  CALL W3FB14(XI,XJ,ALAT1,ELON1,DX,ELONV,ALATAN1,ALATAN2,ALAT,ELON,IERR,
!                                   IERR)
!   INPUT ARGUMENT LIST:
!     XI       - I COORDINATE OF THE POINT  REAL*4
!     XJ       - J COORDINATE OF THE POINT  REAL*4
!     ALAT1    - LATITUDE  OF LOWER LEFT POINT OF GRID (POINT 1,1)
!                LATITUDE <0 FOR SOUTHERN HEMISPHERE; REAL*4
!     ELON1    - LONGITUDE OF LOWER LEFT POINT OF GRID (POINT 1,1)
!                  EAST LONGITUDE USED THROUGHOUT; REAL*4
!     DX       - MESH LENGTH OF GRID IN METERS AT TANGENT LATITUDE
!     ELONV    - THE ORIENTATION OF THE GRID.  I.E.,
!                THE EAST LONGITUDE VALUE OF THE VERTICAL MERIDIAN
!                WHICH IS PARALLEL TO THE Y-AXIS (OR COLUMNS OF
!                THE GRID) ALONG WHICH LATITUDE INCREASES AS
!                THE Y-COORDINATE INCREASES.  REAL*4
!                THIS IS ALSO THE MERIDIAN (ON THE OTHER SIDE OF THE
!                TANGENT CONE) ALONG WHICH THE CUT IS MADE TO LAY
!                THE CONE FLAT.
!     ALATAN1  - THE 1ST LATITUDE AT WHICH THE LAMBERT CONE IS TANGENT TO
!                (TOUCHES OR OSCULATES) THE SPHERICAL EARTH.
!                 SET NEGATIVE TO INDICATE A
!                 SOUTHERN HEMISPHERE PROJECTION; REAL*4
!     ALATAN2  - THE 2ND LATITUDE FROM THE POLE AT WHICH THE LAMBERT CONE
!                INTERSECTS THE SPHERICAL EARTH.
!                SET NEGATIVE TO INDICATE A SOUTHERN HEMISPHERE PROJECTION.
!
!   OUTPUT ARGUMENT LIST:
!     ALAT     - LATITUDE IN DEGREES (NEGATIVE IN SOUTHERN HEMI.)
!     ELON     - EAST LONGITUDE IN DEGREES, REAL*4
!     IERR     - .EQ. 0   IF NO PROBLEM
!                .GE. 1   IF THE REQUESTED XI,XJ POINT IS IN THE
!                         FORBIDDEN ZONE, I.E. OFF THE LAMBERT MAP
!                         IN THE OPEN SPACE WHERE THE CONE IS CUT.
!                  IF IERR.GE.1 THEN ALAT=999. AND ELON=999.
!
!   REMARKS: FORMULAE AND NOTATION LOOSELY BASED ON HOKE, HAYES,
!     AND RENNINGER'S "MAP PROJECTIONS AND GRID SYSTEMS...", MARCH 1981
!     AFGWC/TN-79/003
!
! ATTRIBUTES:
!   LANGUAGE: CRAY CFT77 FORTRAN
!   MACHINE:  CRAY C916-128, CRAY Y-MP8/864, CRAY Y-MP EL2/256
!$$$
         implicit none
         real ALAT,ELON,ALAT1,ELON1,DX,ELONV,ALATAN1,ALATAN2,XI,XJ
         real RERTH,PI,OLDRML,H,PIBY2,RADPD,DEGPRD,REBYDX,ALATN1,ALATN2
         real AN,COSLTN,ELON1L,ELONVR,ALA1,PSI,RMLL,ELO1,ARG,POLEI,POLEJ
         real XX,YY,R2,THETA,BETA,ANINV,ANINV2,STH
         integer IERR
         logical NEWMAP
!
         RERTH  = 6.3712E+6
         PI     = 3.14159
         OLDRML = 99999.
!
!        PRELIMINARY VARIABLES AND REDIFINITIONS
!
!        H = 1 FOR NORTHERN HEMISPHERE; = -1 FOR SOUTHERN
!
         IF (ALATAN1.GT.0) THEN
           H = 1.
         ELSE
           H = -1.
         ENDIF
!
         PIBY2  = PI     / 2.0
         RADPD  = PI     / 180.0
         DEGPRD = 1.0    / RADPD
         REBYDX = RERTH  / DX
         ALATN1 = ALATAN1 * RADPD
         ALATN2 = ALATAN2 * RADPD
         IF (ALATAN1.EQ.ALATAN2) THEN
            AN     = H * SIN(ALATN1)
         ELSE
           AN=LOG(COS(ALATN1)/COS(ALATN2))/ &
             LOG(TAN(((H*PI/2.)-ALATN1)/2.)/TAN(((H*PI/2.)-ALATN2)/2.))
         ENDIF
         COSLTN = COS(ALATN2)
!
!        MAKE SURE THAT INPUT LONGITUDE DOES NOT PASS THROUGH
!        THE CUT ZONE (FORBIDDEN TERRITORY) OF THE FLAT MAP
!        AS MEASURED FROM THE VERTICAL (REFERENCE) LONGITUDE
!
         ELON1L = ELON1
         IF ((ELON1-ELONV).GT.180.) &
          ELON1L = ELON1 - 360.
         IF ((ELON1-ELONV).LT.(-180.)) &
          ELON1L = ELON1 + 360.
!
         ELONVR = ELONV * RADPD
!
!        RADIUS TO LOWER LEFT HAND (LL) CORNER
!
         ALA1 =  ALAT1 * RADPD
!         RMLL = REBYDX * ((COSLTN**(1.-AN))*(1.+AN)**AN) * &
!                (((COS(ALA1))/(1.+H*SIN(ALA1)))**AN)/AN
         PSI=(REBYDX*COSLTN)/(AN*(TAN((PI/4.)-(H*ALATN2/2.))**AN))
         RMLL=PSI*(TAN((PI/4.)-(H*ALA1/2.))**AN)
!
!        USE RMLL TO TEST IF MAP AND GRID UNCHANGED FROM PREVIOUS
!        CALL TO THIS CODE.  THUS AVOID UNNEEDED RECOMPUTATIONS.
!
         IF (RMLL.EQ.OLDRML) THEN
           NEWMAP = .FALSE.
         ELSE
           NEWMAP = .TRUE.
           OLDRML = RMLL
!
!          USE LL POINT INFO TO LOCATE POLE POINT
!
           ELO1 = ELON1L * RADPD
           ARG = AN * (ELO1-ELONVR)
           POLEI = 1. - H * RMLL * SIN(ARG)
           POLEJ = 1. + RMLL * COS(ARG)
         ENDIF
!
!        RADIUS TO THE I,J POINT (IN GRID UNITS)
!              YY REVERSED SO POSITIVE IS DOWN
!
         XX = XI - POLEI
         YY = POLEJ - XJ
         R2 = XX**2 + YY**2
!
!        CHECK THAT THE REQUESTED I,J IS NOT IN THE FORBIDDEN ZONE
!           YY MUST BE POSITIVE UP FOR THIS TEST
!
         THETA = PI*(1.-AN)
         BETA = ABS(ATAN2(XX,-YY))
         IERR = 0
         IF (BETA.LE.THETA) THEN
           IERR = 1
           ALAT = 999.
           ELON = 999.
           IF (.NOT.NEWMAP)  RETURN
         ENDIF
!
!        NOW THE MAGIC FORMULAE
!
         IF (R2.EQ.0) THEN
           ALAT = H * 90.0
           ELON = ELONV
         ELSE
!
!          FIRST THE LONGITUDE
!
           ELON = ELONV + DEGPRD * ATAN2(H*XX,YY)/AN
           ELON = AMOD(ELON+360., 360.)
!
!          NOW THE LATITUDE
!          RECALCULATE THE THING ONLY IF MAP IS NEW SINCE LAST TIME
!
           IF (NEWMAP) THEN
             ANINV = 1./AN
             ANINV2 = ANINV/2.
!             THING = ((AN/REBYDX) ** ANINV)/ &
!              ((COSLTN**((1.-AN)*ANINV))*(1.+ AN))
           ENDIF
           
           STH=sqrt(r2)
!           ALAT = H*(PIBY2 - 2.*ATAN(THING*(R2**ANINV2)))*DEGPRD
           ALAT = H*(PIBY2 - 2.*ATAN(((sth/psi))**ANINV))*DEGPRD
         ENDIF
!
!        FOLLOWING TO ASSURE ERROR VALUES IF FIRST TIME THRU
!         IS OFF THE MAP
!
         IF (IERR.NE.0) THEN
           ALAT = 999.
           ELON = 999.
           IERR = 2
         ENDIF
         RETURN   
       END SUBROUTINE W3FB14
       
end module apm_mapping_mod
