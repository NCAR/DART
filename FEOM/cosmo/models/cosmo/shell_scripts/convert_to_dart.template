#!/bin/csh
#
# DART software - Copyright 2004 - 2011 UCAR. This open source software is
# provided by UCAR, "as is", without charge, subject to all terms of use at
# http://www.image.ucar.edu/DAReS/DART/DART_download
#
# $Id$
#
# batch script to convert all N ensemble members of COSMO grib files
# to filter/dart restart files
#
#==================================================================
#BSUB -J JOB_NAME 
#BSUB -o JOB_NAME.%J.log
#BSUB -P PROJ_NUMBER
#BSUB -W 00:10
#BSUB -q standby
#BSUB -n NPROCS
#BSUB -x
#==================================================================

set myname = $0     # this is the name of this script
set start_time = `date +%s`
echo "host is " `hostname`

# we are going to run the conversions in parallel so make a separate
# subdir for each member
set tempdir = advance_temp

# grab the intended input and output filenames from the input.nml namelist template file
set line      = `fgrep -A 8 \&model_nml input.nml.template | fgrep cosmo_filename`
set inputfile = `echo $line | sed -e 's/^.*cosmo_filename.*=//' -e 's/"//g' -e 's/,//' `

set line       = `fgrep -A 40 \&filter_nml input.nml.template | fgrep restart_in_file_name`
set outputfile = `echo $line | sed -e 's/^.*restart_in_file_name.*=//' -e 's/"//g' -e 's/,//' `

set line       = `fgrep -A 40 \&filter_nml input.nml.template | fgrep ens_size`
set num_ens    = `echo $line | sed -e 's/^.*ens_size.*=//' -e 's/"//g' -e 's/,//' `

# this is fixed in the converter
set convertout = dart.ud

echo num_ens $num_ens
echo inputfile $inputfile
echo outputfile $outputfile

#  copy files to appropriate location, convert them, and copy them back

set mem = 1
while ( $mem < $num_ens )
   #  convert member N
   if ( ! -d ${tempdir}${mem} ) mkdir ${tempdir}${mem}

   # cd into working dir per member
   cd ${tempdir}${mem}

   # copy the template file into a local input.nml
   cp ../input.nml.template ./input.nml

   # construct a set of input filenames of the form:   base.NNNN 
   # where NNNNs are the ensemble number with leading 0s
   set input = `printf %s.%04d $inputfile $mem`
   ln ../$input ./$inputfile

   # do the conversion here, in background
   echo ../cosmo_to_dart &      # FIXME: remove echo to do real conversion

   # and cd back up to the directory where we started
   cd ..

   @ mem ++
end

# all these run in background - wait until all the conversions are finished
wait

# and now copy them back to the working directory
set mem = 1
while ( $mem < $num_ens )

   # cd into working dir per member
   cd ${tempdir}${mem}

   # if the output file is not there, the conversion failed
   if ( ! -e $convertout ) then
      echo conversion failed for ensemble member $mem
      echo looking for file ${tempdir}${mem}/$convertout
      exit -4
   endif

   # construct a set of output filenames of the form:  base.NNNN
   # where the NNNNs are the ensemble number with leading 0s
   set output = `printf %s.%04d $outputfile $mem`
   cp $convertout ../$output

   # and cd back up to the directory where we started
   cd ..

   @ mem ++
end

set end_time = `date  +%s`
@ length_time = $end_time - $start_time
echo "duration_sec = $length_time"

exit 0

# <next few lines under version control, do not edit>
# $URL$
# $Revision$
# $Date$


