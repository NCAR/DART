#!/bin/csh
#
# DART software - Copyright 2004 - 2013 UCAR. This open source software is
# provided by UCAR, "as is", without charge, subject to all terms of use at
# http://www.image.ucar.edu/DAReS/DART/DART_download
#
# DART $Id: mkmf_error_handler_test 9456 2016-01-07 19:04:21Z hkershaw $
#
# usage: mkmf_error_handler_test [ -mpi | -nompi ]
#
# without any args, builds error_handler_test without mpi libraries, and it will run
# as a normal executable. if -mpi is given, it will be compiled with the mpi
# libraries and can run with multiple cooperating processes.

if ( $#argv > 0 ) then
  if ("$argv[1]" == "-mpi") then
    setenv usingmpi 1
  else if ("$argv[1]" == "-nompi") then
    setenv usingmpi 0
  else
    echo "Unrecognized argument to mkmf_error_handler_test: $argv[1]"
    echo "Usage: mkmf_error_handler_test [ -mpi | -nompi ]"
    echo " default is to generate a Makefile without MPI support."
    exit -1
  endif
else
  setenv usingmpi 0
endif


# make a backup copy of the path_names file, and then use
# sed to make sure it includes either the non-mpi subroutines,
# or the subroutines which really call mpi.
cp -f path_names_error_handler_test path_names_error_handler_test.back

if ( $usingmpi ) then

  echo "Making Makefile with MPI"
  touch using_mpi_for_error_handler_test
  sed -e 's;/null_mpi_util;/mpi_util;' path_names_error_handler_test.back >! path_names_error_handler_test

  setenv wrapper_arg -w

else

  echo "Making Makefile without MPI"
  rm -f using_mpi_for_error_handler_test
  sed -e 's;/mpi_util;/null_mpi_util;' path_names_error_handler_test.back >! path_names_error_handler_test
  setenv wrapper_arg ""

endif

# remove temp file and now really call mkmf to generate makefile
rm -f path_names_error_handler_test.back

../../../mkmf/mkmf -p error_handler_test -t ../../../mkmf/mkmf.template   \
        -a "../../.." ${wrapper_arg} path_names_error_handler_test

exit $status
